[gd_resource type="Shader" format=3 uid="uid://fuw3eajvygqp"]

[resource]
code = "shader_type spatial;
render_mode vertex_lighting, shadows_disabled, blend_mix, diffuse_lambert;

uniform vec4 modulate: source_color = vec4(1,1,1,1);
uniform vec4 glow_modulate: source_color = vec4(1,1,1,1);
uniform float glow_pulse = 0;
uniform float offset = 0;
uniform sampler2D image_texture: source_color;
uniform sampler2D emission_texture: hint_default_black;
uniform vec2 image_size = vec2(64,64);
uniform vec2 uv_scale = vec2(1.0);
uniform vec2 uv_offset = vec2(0.0);
uniform vec2 uv_scrolling_velocity = vec2(0.0);
uniform float alpha_scissor = 0;

const float vertex_snapping_offset = 0.8;
const vec2 resolution = vec2(960,720);

void vertex() {
	vec4 base_position = PROJECTION_MATRIX * MODELVIEW_MATRIX * vec4(VERTEX, 1.0);
	vec4 snapped_position = base_position;
	snapped_position.xyz = base_position.xyz / base_position.w;

	vec2 snap_resolution = floor(resolution*(1.0 - vertex_snapping_offset));
	snapped_position.x = floor(snap_resolution.x * snapped_position.x) / snap_resolution.x;
	snapped_position.y = floor(snap_resolution.y * snapped_position.y) / snap_resolution.y;

	snapped_position.xyz *= base_position.w;
	POSITION = snapped_position;
}

void fragment() {
	vec2 mod_uv = UV * uv_scale + uv_offset + (uv_scrolling_velocity*TIME);
	vec4 texture_sample = texture(image_texture, mod_uv);
	vec4 emissive_sample = texture(emission_texture, mod_uv);

    ALBEDO = mix(COLOR.rgb, texture_sample.rgb, texture_sample.a) * modulate.rgb;
	EMISSION = emissive_sample.rgb * emissive_sample.a * (sin(TIME*glow_pulse+offset)+1.0)/2.0 * glow_modulate.rgb;
	ALPHA = COLOR.a*modulate.a;
    ALPHA_SCISSOR_THRESHOLD = alpha_scissor;
}"
